'** Custom Sprite Bank Merger v0.1 **  
'   Author: Alper Sönmez (27.03.2021) v0.2 
' -- Appends two Custom Sprite Banks together -- 

'File parameters 
_FILE1$="sprites_R.spr"
_FILE2$="sprites_L.spr"
_SAVE$="sprites.spr"

'Calculate the size of the new bank
Open In 1,_FILE1$
   _SIZE1=Lof(1)
Close 1
Open In 1,_FILE2$
   _SIZE2=Lof(1)
Close 1

'2 words for num. images and image data size will be combined, hence: -4 
_SIZE_BANK=_SIZE1+_SIZE2-4

'Load both banks into memory 
Reserve As Work 5,_SIZE1
Bload _FILE1$,Start(5)
Reserve As Work 6,_SIZE2
Bload _FILE2$,Start(6)

'Allocate memory for the merged bank 
Reserve As Work 7,_SIZE_BANK

_NUM_IMAGES1=Deek(Start(5))
_NUM_IMAGES2=Deek(Start(6))
_TABLE1_PT=Start(5)+2
_TABLE2_PT=Start(6)+2
_IMAGES1_SIZE_PT=_TABLE1_PT+(_NUM_IMAGES1*4)
_IMAGES2_SIZE_PT=_TABLE2_PT+(_NUM_IMAGES2*4)
_IMAGES1_SIZE=Deek(_IMAGES1_SIZE_PT)
_IMAGES2_SIZE=Deek(_IMAGES2_SIZE_PT)
_IMAGES1_PT=_IMAGES1_SIZE_PT+2
_IMAGES2_PT=_IMAGES2_SIZE_PT+2

_TABLE_BANK_PT=Start(7)+2

'Set new num images
Doke Start(7),_NUM_IMAGES1+_NUM_IMAGES2

'Copy the first table
Copy _TABLE1_PT,_IMAGES1_SIZE_PT To _TABLE_BANK_PT

'Copy the second table 
RA=_TABLE2_PT
WA=_TABLE_BANK_PT+(_NUM_IMAGES1*4)
For I=0 To _NUM_IMAGES2
   OFFS=Deek(RA)+_IMAGES1_SIZE
   If OFFS>$FFFF
      Goto _ERROR
   End If 
   Doke WA,OFFS
   Add RA,2 : Add WA,2
   Doke WA,Deek(RA)
   Add RA,2 : Add WA,2
Next I

'Set the final offset (_IMAGES_SIZE of the new Bank)   
Doke WA,Deek(RA)+_IMAGES1_SIZE
Add WA,2

'Copy first image data 
Copy _IMAGES1_PT,_IMAGES1_PT+_IMAGES1_SIZE To WA
Add WA,_IMAGES1_SIZE

'Copy second image data
Copy _IMAGES2_PT,_IMAGES2_PT+_IMAGES2_SIZE To WA

Erase 5
Erase 6

Bsave _SAVE$,Start(7) To Start(7)+_SIZE_BANK
Erase 7

End 

_ERROR:
Print "Sprite bank too big!"
Erase 5 : Erase 6 : Erase 7
End 
